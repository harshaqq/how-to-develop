plugins {
  id "mpern.sap.commerce.build" version "1.2.0"
}
import mpern.sap.commerce.build.tasks.HybrisAntTask
import java.nio.file.Files

hybris {
    version = '6.7.0.8'
}

repositories {
    jcenter()
    flatDir {
        dirs "${rootProject.projectDir}/libs"
    }
}

configurations {
    hotswapagent
}

dependencies {
    hotswapagent('org.hotswapagent:hotswap-agent:1.3.0') {
        transitive = false
    }
}

task downloadHotSwapAgent(type: Copy) {
    from configurations.hotswapagent
    into file("hybris/config")
}

//to avoid checking in the generated module, create it on demand:
task disableLocalExtensions {
    doLast {
        Files.move(file("hybris/config/localextensions.xml").toPath(), file("hybris/config/localextensions.xml.off").toPath())
    }
}
task setupStorefront(type: HybrisAntTask, dependsOn: ["disableLocalExtensions"]) {
    onlyIf {
        !file("hybris/bin/custom/demo").exists()
    }
    args("modulegen")
    antProperty("input.module", "accelerator")
    antProperty("input.name", "demo")
    antProperty("input.package", "com.demo")
}
setupStorefront.mustRunAfter "bootstrapPlatform"
task enableLocalExtensions {
    doLast {
        if (file("hybris/config/localextensions.xml.off").exists()) {
            Files.move(file("hybris/config/localextensions.xml.off").toPath(), file("hybris/config/localextensions.xml").toPath())
        }
    }
}
task installCowAddon(type: HybrisAntTask, dependsOn: ["enableLocalExtensions"]) {
    args("addoninstall")
    antProperty("addonnames", "cowaddon")
    antProperty("addonStorefront.yacceleratorstorefront", "demostorefront")
}

installCowAddon.mustRunAfter setupStorefront
task bootstrap(dependsOn: ["bootstrapPlatform", "setupStorefront", "installCowAddon"]) {
    group "Project"
    description "Bootstrap this project"
}

task unitTests(type: HybrisAntTask) {
    args("unittests")
    antProperty("testclasses.extensions", "cowsaytests")
    antProperty("testclasses.suppress.junit.tenant", "true")
}

//make sure the junit tenant is properly initialized before running the integration tests
//ant yunitinit -Dde.hybris.platform.ant.production.skip.build=true
task integrationtests(type: HybrisAntTask) {
    args("integrationtests")
    antProperty("testclasses.extensions", "cowsaytests")
}

task storefronttests(type: HybrisAntTask) {
    args("alltests")
    antProperty("testclasses.web", "true")
    antProperty("testclasses.extensions", "demostorefront")
}